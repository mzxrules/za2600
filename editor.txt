load char set "@48digits.chr"
load char set "@65letters.chr"
load char set "@96lowcase.chr"
lockplayer
wait 1
. "init ex data"
loop start
set "zz&loopcount&" "loopcount"
loop 255

set "mzx_speed" to 8

. "mzx sprite reference"
loop start
put c00 CustomBlock "('loopcount'*2)" at "('loopcount'%64)" "('loopcount'/64*2)"
put c00 CustomBlock "('loopcount'*2+1)" at "('loopcount'%64)" "('loopcount'/64*2+1)"
loop for 127

. "UI Sprite Init"
loop start
set "l" to 0
: "loop"
copy char "('loopcount'*8+'l'+'SS_UI')" to "('l'+'SPR_WORK')"
inc "l" by 1
if "l" < 8 then "loop"
set "l" to 0
: "loop2"
copy char "('l'+'SPR_WORK')" to "('loopcount'*8+('l'*2)+'SPR_UI')"
copy char "('l'+'SPR_WORK'+4)" to "('loopcount'*8+('l'*2)+1+'SPR_UI')"
inc "l" by 1
if "l" < 4 then "loop2"
loop for "(32 * 3 - 1)"
zap "loop" 1
zap "loop2" 1

. "Extend Room Sprite Sheet Top/Bottom"
loop start
set "sx" "('loopcount'%32)"
set "sy" "('loopcount'/32*64+'SS_ROOM')"

set "char" ('sx'+'sy')
set "byte" 2
set "c_b" "char_byte"
set "byte" 0
set "char_byte" "c_b"
set "byte" 1
set "char_byte" "c_b"
set "char" ('sx'+'sy'+32)
set "byte" 3
set "c_b" "char_byte"
set "byte" 4
set "char_byte" "c_b"
set "byte" 5
set "char_byte" "c_b"
loop 63

. "Room Sprite Sheet"
. "convert PF sprite char order from (0 32), (1, 33) to (0, 1), (2, 3)"
. "additionally, mirror PF1 sprites"
loop start
set "sx" to "('loopcount'%32)"
set "sy" to "('loopcount'/32*64+'SS_ROOM')"
set "tx" to "('loopcount'*2+'SPR_PF1')"

copy char "('sx'+'sy')" to "('tx')"
copy char "('sx'+'sy'+32)" to "('tx'+1)"
loop 63

loop start
set "sx" to "('loopcount'%32)"
set "sy" to "('loopcount'/32*64+'SS_ROOM')"
set "tx" to "('loopcount'*2+'SPR_PF2')"

copy char "('sx'+'sy')" to "('tx')"
copy char "('sx'+'sy'+32)" to "('tx'+1)"
loop 63

loop start
flip char "('SPR_PF1'+'loopcount'+64)" EAST
loop 127

. "initialize unbounded mzx sprites 0-127 are used for world rooms"
loop start
set "spr&loopcount&_unbound" to 1
set "spr&loopcount&_offset" to "('loopcount'/32*256+'SPR_WORK')"
set "spr&loopcount&_width" to 4
set "spr&loopcount&_height" to 2
set "spr&loopcount&_refx" to "('loopcount'%16*4)"
set "spr&loopcount&_refy" to "('loopcount'/16%2*2)"
set "spr&loopcount&_tcol" to -1
loop for 127
. "initialize unbounded mzx sprite for cursor"
set "spr128_unbound" to 1
set "spr128_offset" to "('SPR_UI'+8)"
set "spr128_width" to 4
set "spr128_height" to 2
set "spr128_refx" to 0
set "spr128_refy" to 0

set "worldId" to 0
goto "#load_data"
goto "#set_world"

: "q"
inc "frametimer" by 1
wait for 1
if leftpressed then "#left"
if rightpressed then "#right"
if uppressed then "#up"
if downpressed then "#down"
if spacepressed then "#space"
if "key_pressed" = 127 then "#delete"

loop start
set "tilecolor" to "('$lvlco.&loopcount&')"
put "('tilecolor')" Sprite "('loopcount')" at "('loopcount'%'lmod'*33+'lvlx')" "('loopcount'/'lmod'*21+56)"
if "loopcount" != "roomId" then "sprite_draw_next"
set "curcolor" to "('tilecolor' a 0x0F = 0x0F * -7+15)"
set "curcolor" to "('frametimer'a0x4>0*'curcolor')"
put "('curcolor')" Sprite p80 at "('loopcount'%'lmod'*33+'lvlx')" "('loopcount'/'lmod'*21+56)"
: "sprite_draw_next"
loop for "('lcell'-1)"
goto "q"

: "keyq"
set "roomTyL" to "$lvl.('roomId'*3)"
set "roomTyL" to "('roomTyL'+1%32)"
set "$lvl.('roomId'*3)" to "roomTyL"
goto "#update_room"
goto "q"

: "keyw"
set "roomTyM" to "$lvl.('roomId'*3+2)"
set "roomTyM" to "('roomTyM'+1%64)"
set "$lvl.('roomId'*3+2)" to "roomTyM"
goto "#update_room"
goto "q"

: "keye"
set "roomTyR" to "$lvl.('roomId'*3+1)"
set "roomTyR" to "('roomTyR'+1%32)"
set "$lvl.('roomId'*3+1)" to "roomTyR"
goto "#update_room"
goto "q"

: "keya"
set "roomTyL" to "$lvl.('roomId'*3)"
set "roomTyL" to "('roomTyL'+31%32)"
set "$lvl.('roomId'*3)" to "roomTyL"
goto "#update_room"
goto "q"

: "keys"
set "roomTyM" to "$lvl.('roomId'*3+2)"
set "roomTyM" to "('roomTyM'+63%64)"
set "$lvl.('roomId'*3+2)" to "roomTyM"
goto "#update_room"
goto "q"

: "keyd"
set "roomTyR" to "$lvl.('roomId'*3+1)"
set "roomTyR" to "('roomTyR'+31%32)"
set "$lvl.('roomId'*3+1)" to "roomTyR"
goto "#update_room"
goto "q"

: "keyr"
set "$lvlrs.&roomId&" "('$lvlrs.&roomId&'+1%'RsCount')"
goto "q"

: "keyt"
set "$lvlrs.&roomId&" "('$lvlrs.&roomId&'+('RsCount'-1)%'RsCount')"
goto "q"

: "keyf"
set "$lvlex.&roomId&" "('$lvlex.&roomId&'+1%256)"
goto "q"

: "keyg"
set "$lvlex.&roomId&" "('$lvlex.&roomId&'+255%256)"
goto "q"

: "keyp"
ask "Save World Data?"
: "NO"
goto "q"

: "YES"
set "$w&worldId&" to "$lvl"
set "$w&worldId&co" to "$lvlco"
set "$w&worldId&door0" to "$lvldoor0"
set "$w&worldId&door1" to "$lvldoor1"
set "$w&worldId&door2" to "$lvldoor2"
set "$w&worldId&door3" to "$lvldoor3"
set "$w&worldId&rs" to "$lvlrs"
set "$w&worldId&ex" to "$lvlex"

loop start
set "world/w&loopcount&.bin" to "fwrite_open"
set "$w&loopcount&" to "fwrite('$w&loopcount&.length')"
set "" to "fwrite_open"
set "world/w&loopcount&co.bin" to "fwrite_open"
set "$w&loopcount&co" to "fwrite('$w&loopcount&co.length')"
set "" to "fwrite_open"
set "world/w&loopcount&door.bin" to "fwrite_open"
set "$w&loopcount&door0" to "fwrite('$w&loopcount&door0.length')"
set "" to "fwrite_open"
set "world/w&loopcount&door.bin" to "fwrite_open"
set "$w&loopcount&door1" to "fwrite('$w&loopcount&door1.length')"
set "" to "fwrite_open"
set "world/w&loopcount&door.bin" to "fwrite_open"
set "$w&loopcount&door2" to "fwrite('$w&loopcount&door2.length')"
set "" to "fwrite_open"
set "world/w&loopcount&door.bin" to "fwrite_open"
set "$w&loopcount&door3" to "fwrite('$w&loopcount&door3.length')"
set "" to "fwrite_open"
set "world/w&loopcount&rs.bin" to "fwrite_open"
set "$w&loopcount&rs" to "fwrite('$w&loopcount&rs.length')"
set "" to "fwrite_open"
set "world/w&loopcount&ex.bin" to "fwrite_open"
set "$w&loopcount&ex" to "fwrite('$w&loopcount&ex.length')"
set "" to "fwrite_open"
goto "#write_rsex"
loop for 2
* "~FSAVED"
goto "q"

: "#write_rsex"
set "l" 0
set "world/w&loopcount&rsex.txt" "fwrite_open"
: "write_rsex_loop"
set "d1" "$w&loopcount&rs.&l&"
set "d2" "$w&loopcount&ex.&l&"
if "d1" >= "RsCount" "#write_rsex_default"
set "$" "$Rs&d1&"
set "$" "fwrite"
set "exjump" 0
if "$" === "RsItem" "#write_ex_RsItem"
if "exjump" == 0 "#write_ex_default"
inc "l" 1
if "l" < 128 "write_rsex_loop"
set "" "fwrite_open"
goto "#return"

: "#write_ex_default"
set "$" "zz&d2&"
set "$" "fwrite"
goto "#return"

: "#write_ex_RsItem"
set "exjump" 1
set "ex" 0
: "write_ex_RsItem_l"
inc "ex" 1
if "zz&d2&" != "('&$Gi('ex')&')" "write_ex_RsItem_c"
set "$" "&$Gi('ex')&"
set "$" "fwrite"
goto "#return"
: "write_ex_RsItem_c"
if "ex" < "GiCount" "write_ex_RsItem_l"
set "exjump" 0
goto "#return"

: "#write_rsex_default"
set "d1" 0
goto "#return"

: "#load_rsex"
set "world/w&loopcount&rsex.txt" to "fread_open"
set "l" 0
: "load_rsex_loop"
set "$" to "fread"
goto "#load_rsex_rscheck"
set "$w&loopcount&rs.&l&" ('&$&')
set "$" to "fread"
set "$w&loopcount&ex.&l&" ('&$&')
inc "l" 1
if "l" < 128 "load_rsex_loop"
set "" to "fread_open"
goto "#return"

: "#load_rsex_rscheck"
set "l1" 0
: "load_rsex_rscheck"
if "$" === "$Rs&l1&" then "load_rsex_rscheck_pass"
inc "l1" 1
if "l1" < "RsCount" "load_rsex_rscheck"
zap "keyp" 1
* "~f&$& IS UNDEFINED"
set "" to "fread_open"
die
: "load_rsex_rscheck_pass"
goto "#return"

: "#load_data"
loop start
set "world/w&loopcount&.bin" to "fread_open"
set "$w&loopcount&" to "fread('fread_length')"
set "" to "fread_open"
set "world/w&loopcount&co.bin" to "fread_open"
set "$w&loopcount&co" to "fread('fread_length')"
set "" to "fread_open"
set "world/w&loopcount&door0.bin" to "fread_open"
set "$w&loopcount&door0" to "fread('fread_length')"
set "" to "fread_open"
set "world/w&loopcount&door1.bin" to "fread_open"
set "$w&loopcount&door1" to "fread('fread_length')"
set "" to "fread_open"
set "world/w&loopcount&door2.bin" to "fread_open"
set "$w&loopcount&door2" to "fread('fread_length')"
set "" to "fread_open"
set "world/w&loopcount&door3.bin" to "fread_open"
set "$w&loopcount&door3" to "fread('fread_length')"
set "" to "fread_open"
goto "#load_rsex"
loop for 2
goto "#return"

: "keym"
set "$w&worldId&" to "$lvl"
set "$w&worldId&co" to "$lvlco"
set "$w&worldId&door0" to "$lvldoor0"
set "$w&worldId&door1" to "$lvldoor1"
set "$w&worldId&door2" to "$lvldoor2"
set "$w&worldId&door3" to "$lvldoor3"
set "$w&worldId&rs" "$lvlrs"
set "$w&worldId&ex" "$lvlex"
set "worldId" to "('worldId'+1%3)"
goto "#set_world"
goto "q"

: "keyl"
input string "Door Flags"
set "$" to "input"
loop start
set "doors" to "('$.&loopcount&'-48 a 7)"
. "TODO: clamp values"
set "$lvldoor.&roomId&" to "('doors')"
loop for 3
goto "#update_room"
goto "q"

: "keyz"
* "~fCOPY"
set "$copy_roomTy" to "$lvl+('roomId'*3)#3"
set "copy_roomCo" to "('$lvlco.&roomId&')"
set "copy_roomDoor0" to "('$lvldoor0.&roomId&')"
set "copy_roomDoor1" to "('$lvldoor1.&roomId&')"
set "copy_roomDoor2" to "('$lvldoor2.&roomId&')"
set "copy_roomDoor3" to "('$lvldoor3.&roomId&')"
goto "q"

: "#space"
* "~fPASTE"
if "copy_roomCo" = 0 then "#return"
set "$lvl+('roomId'*3)" to "$copy_roomTy#3"
set "$lvlco.&roomId&" to "copy_roomCo"
set "$lvldoor0.&roomId&" to "copy_roomDoor0"
set "$lvldoor1.&roomId&" to "copy_roomDoor1"
set "$lvldoor2.&roomId&" to "copy_roomDoor2"
set "$lvldoor3.&roomId&" to "copy_roomDoor3"
goto "#update_room"
goto "#return"

: "keyc"
input string "Color"
set "$" to "input"
if "$.length" != 2 then "q"
set "ascii" to "$.0"
goto "#ascii_to_hex"
set "co" to "('ascii'<<4)"
set "ascii" to "$.1"
goto "#ascii_to_hex"
inc "co" by "ascii"
set "$lvlco.&roomId&" to "co"
goto "q"

: "#delete"
set "$lvlco.&roomId&" to 0
goto "#return"

: "#ascii_to_hex"
if "ascii" < 48 then "ascii_to_hex_0"
if "ascii" < 58 then "ascii_to_hex_n"
if "ascii" < 65 then "ascii_to_hex_0"
if "ascii" <= 70 then "ascii_to_hex_l"
if "ascii" < 97 then "ascii_to_hex_0"
if "ascii" < 103 then "ascii_to_hex_l"
: "ascii_to_hex_0"
set "ascii" to 0
goto "#return"
: "ascii_to_hex_n"
dec "ascii" by 48
goto "#return"
: "ascii_to_hex_l"
set "ascii" to "('ascii'-1%6+10)"
goto "#return"


: "key1"
. "north door"
. "doors & 0x03"
set "$lvldoor0.&roomId&" "('$lvldoor0.&roomId&' + 1 % ('worldId'=0+6))"
goto "#update_room"
goto "q"

: "key2"
. "south door"
. "doors & 0x0C"
set "$lvldoor1.&roomId&" "('$lvldoor1.&roomId&' + 1 % ('worldId'=0+6))"
goto "#update_room"
goto "q"

: "key4"
. "east door"
. "doors & 0x30"
set "$lvldoor2.&roomId&" "('$lvldoor2.&roomId&' + 1 % ('worldId'=0+6))"
goto "#update_room"
goto "q"

: "key3"
. "west door"
. "doors & 0xC0"
set "$lvldoor3.&roomId&" "('$lvldoor3.&roomId&' + 1 % ('worldId'=0+6))"
goto "#update_room"
goto "q"


: "#up"
set "roomId" to "('roomId'+'lcell'-'lmod'%'lcell')"
set "frametimer" to 4
goto "#return"
: "#down"
set "roomId" to "('roomId'+'lmod'%'lcell')"
set "frametimer" to 4
goto "#return"
: "#left"
set "roomId" to "('roomId'/'lmod'*'lmod'+('roomId'+'lmod'-1%'lmod'))"
set "frametimer" to 4
goto "#return"
: "#right"
set "roomId" to "('roomId'/'lmod'*'lmod'+('roomId'+1%'lmod'))"
set "frametimer" to 4
goto "#return"


: "#update_room"
. "roomId"
. "roomTy"
set "roomTyL" to "('$lvl.('roomId'*3)' a 31)"
loop start
copy char "('roomTyL'*2+'loopcount'+'SPR_PF1')" to "('roomId'*8+'loopcount'+'SPR_WORK')"
loop for 1
set "roomTyR" to "('$lvl.('roomId'*3+1)'a0x1F)"
loop start
copy char "('roomTyR'*2+'loopcount'+'SPR_PF2')" to "('roomId'*8+'loopcount'+'SPR_WORK'+6)"
loop for 1
set "roomTyM" to "('$lvl.('roomId'*3+2)'a0x3F)"
loop start
copy char "('roomTyM'*2+'loopcount'+'SPR_PF2')" to "('roomId'*8+'loopcount'+'SPR_WORK'+2)"
copy char "('roomTyM'*2+'loopcount'+'SPR_PF1')" to "('roomId'*8+'loopcount'+'SPR_WORK'+4)"
loop for 1
set "sprA" "('worldId'=0*3)"
goto "#mask_room_spr"
set "sprA" to "('worldId'=0+ 1)"
goto "#compose_room_spr"
set "lur" to 0
: "update_room_doors"
set "doors" to "$lvldoor&lur&.&roomId&"
if "doors" = 0 then "update_room_doors_skip"
set "sprA" to "('doors'*4 + 'lur' + 32 + ('worldId'=0*32))"
goto "#compose_room_spr"
: "update_room_doors_skip"
inc "lur" by 1
if "lur" < 4 then "update_room_doors"
goto "#return"

: "#mask_room_spr"
set "compose_op" to 0
goto "compose_spr_imp"

: "#compose_room_spr"
set "compose_op" to 1

: "compose_spr_imp"
. "combine sprA to sprB"
set "sprA" to "('SPR_UI'/8+'sprA')"
set "sprB" to "('SPR_WORK'/8+'roomId')"

loop start
set "l" to 0
: "compose"
set "char" to "('sprA'*8+'loopcount')"
set "byte" to "l"
set "cpbyte" to "char_byte"
set "char" to "('sprB'*8+'loopcount')"
if "compose_op" = 0 then "#compose_xor_mask"
if "compose_op" = 1 then "#compose_or"
inc "l" by 1
if "l" < 14 then "compose"
loop for 7
goto "#return"

: "#compose_xor_mask"
set "char_byte" to "('char_byte'a('cpbyte'x255))"
goto "#return"

: "#compose_or"
set "char_byte" to "('char_byte'o'cpbyte')"
goto "#return"

: "#set_world"
set "$lvl" to "$w&worldId&"
set "$lvlco" to "$w&worldId&co"
set "$lvldoor0" to "$w&worldId&door0"
set "$lvldoor1" to "$w&worldId&door1"
set "$lvldoor2" to "$w&worldId&door2"
set "$lvldoor3" to "$w&worldId&door3"
set "$lvlrs" to "$w&worldId&rs"
set "$lvlex" to "$w&worldId&ex"
if "worldId" = 0 then "#draw_16x8"
if "worldId" > 0 then "#draw_16x8"

set "roomId" to 0
: "init_room_spr"
goto "#update_room"
inc "roomId" by 1
if "roomId" < "lcell" then "init_room_spr"
set "roomId" to 0
goto "#return"

: "#draw_8x8"
set "lmod" to 8
set "lvlx" to "(6*33)"
set "lcell" to 64
loop start
set "spr('loopcount'+64)_off" to 1
loop for 63
goto "#return"

: "#draw_16x8"
set "lmod" to 16
set "lvlx" to "(2*33)"
set "lcell" to 128
goto "#return"

: "#mirror_b"
set "b" to "(('b' a 0x55 << 1) o ('b' a 0xAA >> 1))"
set "b" to "(('b' a 0x33 << 2) o ('b' a 0xCC >> 2))"
set "b" to "(('b' a 0x0F << 4) o ('b' a 0xF0 >> 4))"
goto "#return"
